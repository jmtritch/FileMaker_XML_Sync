/*
  ------------------------------------------------------------------------------
  Purpose: Extracts the data from the xml variable that is between the
                 <attribute></attribute> tags
  Author: Andy Knasinski
  Notes: https://www.briandunning.com/cf/1
  Parameters:
         xml - XML formatted data source
         attribute - Taged content within the <content></content> fields
         instance - Instance number of the attribute
  Data Type Returned:
         text
  ------------------------------------------------------------------------------
*/

Let (
	[ 
		xml_length = Length ( xml ) ; 
		attribute_length = Length ( attribute ) ; 
		get_instance = If ( IsEmpty ( instance ) ; 1 ; instance ) 
	];
 
	Case(
		IsEmpty ( xml ) or IsEmpty ( attribute ) or PatternCount ( xml ; "<" & attribute & ">" ) = 0 ; "" ;
		Middle ( xml ; Position ( xml ; "<" & attribute & ">" ; 1 ; get_instance ) + attribute_length + 2 ; 
		xml_length - ( xml_length - Position ( xml ; "</" & attribute & ">" ; 1 ; get_instance ) ) - ( Position ( xml ; "<" & attribute & ">" ; 1 ; get_instance ) + attribute_length + 2 ) )
	)
)